include(GoogleTest)

# Helper files for the tests:
set(HEADERS
)
set(SOURCES
)

# The tests themselves:
set(TEST_SOURCES
  slapi/color_tests.cpp
  image_tests.cpp
)

source_group(TREE ${CMAKE_CURRENT_LIST_DIR} PREFIX "Header Files" FILES ${HEADERS})
source_group(TREE ${CMAKE_CURRENT_LIST_DIR} PREFIX "Source Files" FILES ${SOURCES})
source_group(TREE ${CMAKE_CURRENT_LIST_DIR} PREFIX "Tests" FILES ${TEST_SOURCES})

# TODO(Example): Set the name of the test binary here:
set(TEST_APP_NAME imagelib_tests)

add_executable(${TEST_APP_NAME} ${HEADERS} ${SOURCES} ${TEST_SOURCES})
target_link_libraries(${TEST_APP_NAME} PRIVATE
  project_options
  project_warnings
  gtest_main
  gmock_main
  imagelib_test_static
)
target_include_directories(${TEST_APP_NAME} PRIVATE ${CMAKE_CURRENT_LIST_DIR})

gtest_discover_tests(${TEST_APP_NAME})

# Copy the required SketchUpAPI dependencies required to run the tests into
# the binary directory.
foreach(file_i ${SketchUpAPI_BINARIES})
  add_custom_command(TARGET ${TEST_APP_NAME}
    PRE_BUILD
    COMMAND ${CMAKE_COMMAND}
      ARGS -E copy_if_different ${file_i} ${CMAKE_CURRENT_BINARY_DIR}
    COMMENT "Copy ${file_i} to ${CMAKE_CURRENT_BINARY_DIR} directory" VERBATIM
  )
endforeach(file_i)

# Verify test executable link against standalone C API.
include("DependencyTools")
validate_dependencies(${TEST_APP_NAME}
  EXPECTED SketchUpAPI.dll SketchUpAPI.framework
  UNEXPECTED sketchup.exe
)
